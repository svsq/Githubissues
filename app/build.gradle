apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    def timestampCode = new Date().format('yyMMddHHmm').toInteger()
    compileSdkVersion 29
    buildToolsVersion "29.0.0"
    defaultConfig {
        applicationId "com.paulsoja.githubissues"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        development {
            minifyEnabled false
            debuggable true
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //ext.betaDistributionGroupAliases = "zakazuadev"
        }
        android.applicationVariants.all { variant ->
            def relativeRootDir = variant.getPackageApplicationProvider().get().outputs.files[1].toPath()
                    .relativize(rootDir.toPath()).toFile()
            def baseUrl = System.getenv("BASE_URL") != null ? System.getenv("BASE_URL") :
                    '"https://test.github.issue/"'

            buildConfigField("String", "BASE_URL", baseUrl)
            def buildNameSuffix = variant.buildType.name != "release" ? "-${variant.buildType.name}" : ""
            variant.outputs.all { output ->
                output.outputFileName = new File("$relativeRootDir/apk",
                        "${rootProject.name}_${android.defaultConfig.versionName}${timestampCode}${buildNameSuffix}.apk")
            }
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility '1.8'
        targetCompatibility '1.8'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    androidExtensions {
        experimental = true
    }
    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Android framework
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation "androidx.annotation:annotation:1.1.0"

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "com.google.android.material:material:1.1.0-alpha09"

    implementation "androidx.recyclerview:recyclerview:1.0.0"

    // Room
    implementation "androidx.room:room-runtime:2.1.0"
    implementation "androidx.room:room-rxjava2:2.1.0"
    kapt "androidx.room:room-compiler:2.1.0"

    // Api
    implementation "com.squareup.retrofit2:retrofit:2.5.0"
    implementation "com.squareup.retrofit2:converter-gson:2.5.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.4.0"
    implementation "com.squareup.okhttp3:logging-interceptor:3.11.0"

    implementation "io.reactivex.rxjava2:rxkotlin:2.2.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"
    implementation "com.jakewharton.rxbinding:rxbinding-kotlin:0.4.0"

    // Images
    implementation "com.github.bumptech.glide:okhttp3-integration:4.9.0"
    implementation "com.github.bumptech.glide:glide:4.9.0"
    kapt "com.github.bumptech.glide:compiler:4.9.0"
    implementation("com.github.bumptech.glide:recyclerview-integration:4.9.0") {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }

    // Dagger
    implementation "com.google.dagger:dagger:2.16"
    kapt "com.google.dagger:dagger-compiler:2.16"

    // Mvp
    implementation 'tech.schoolhelper:moxy-x:1.7.0'
    implementation 'tech.schoolhelper:moxy-x-androidx:1.7.0'
    implementation 'tech.schoolhelper:moxy-x-material:1.7.0'
    kapt 'tech.schoolhelper:moxy-x-compiler:1.7.0'

    // Tools
    // debug-db - tool for database inspection DO NOT include into release build.
    // When debugging filter logcat for "in your browser" there will be link which you open in browser for inspection
    // implementation 'com.amitshekhar.android:debug-db:1.0.6'
    implementation 'com.itkacher.okhttpprofiler:okhttpprofiler:1.0.5'
    implementation "ru.terrakok.cicerone:cicerone:5.0.0"
    implementation "com.hannesdorfmann:adapterdelegates3:3.1.0"
    implementation 'com.facebook.shimmer:shimmer:0.4.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
